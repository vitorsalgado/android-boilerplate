apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'kotlin-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.fabric'
apply from: "$rootDir/tools/tools.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.example"

        minSdkVersion 19
        targetSdkVersion 25

        versionCode 1000
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    dataBinding {
        enabled true
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("$rootDir/distribution/debug.keystore")
            storePassword 'android'
        }

        if (System.getenv("KEY_ALIAS") == null) {
            release {}
        } else {
            release {
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"

            minifyEnabled true
            useProguard true
            testCoverageEnabled true

            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }

        qa {
            versionNameSuffix ".qa"

            minifyEnabled true
            useProguard true

            signingConfig signingConfigs.debug

            ext.betaDistributionGroupAliases = "qa"
            ext.betaDistributionReleaseNotesFilePath = "$projectDir/release-notes-qa.txt"
        }

        release {
            minifyEnabled true
            useProguard true
            zipAlignEnabled true
            shrinkResources true

            signingConfig signingConfigs.release

            ext.betaDistributionGroupAliases = "beta"
            ext.betaDistributionReleaseNotesFilePath = "$projectDir/release-notes.txt"
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

}

final versions = rootProject.ext

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Test Libraries

    testCompile "junit:junit:$versions.junit"
    testCompile "org.mockito:mockito-all:$versions.mockito"
    testCompile "org.hamcrest:hamcrest-all:$versions.hamcrest"
    androidTestCompile "com.android.support.test:runner:$versions.testSupportLibrary"
    androidTestCompile "com.linkedin.testbutler:test-butler-library:$versions.testButler"

    // Android Support Libraries

    compile "com.android.support:support-annotations:$versions.supportLibrary"
    compile "com.android.support:appcompat-v7:$versions.supportLibrary"
    compile "com.android.support:design:$versions.supportLibrary"
    compile "com.android.support:support-v4:$versions.supportLibrary"
    compile "com.android.support:support-v13:$versions.supportLibrary"
    compile "com.android.support:recyclerview-v7:$versions.supportLibrary"
    compile "com.android.support:cardview-v7:$versions.supportLibrary"

    // Libs

    // Google
    compile "com.google.android.gms:play-services-location:$versions.play"
    compile "com.google.firebase:firebase-core:$versions.firebase"
    compile "com.google.firebase:firebase-messaging:$versions.firebase"
    compile "com.google.code.gson:gson:$versions.gson"

    // Facebook
    compile "com.facebook.android:facebook-android-sdk:$versions.facebook"
    compile "com.facebook.rebound:rebound:$versions.rebound"

    // RxJava
    compile "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
    compile "io.reactivex.rxjava2:rxjava:$versions.rxJava"

    // Square Up Tools
    compile "com.squareup.picasso:picasso:$versions.picasso"
    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$versions.retrofit"
    compile "com.squareup.okhttp3:okhttp:$versions.okhttp"

    // Butter Knife
    compile "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"

    // Others
    compile "org.greenrobot:eventbus:$versions.eventbus"
    compile "com.android.support.test.espresso:espresso-idling-resource:$versions.espresso"
    compile "de.psdev.licensesdialog:licensesdialog:$versions.licensesDialog"
    compile "com.github.siyamed:android-shape-imageview:$versions.shapeImageView"

    // Development and Support Libs

    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
    qaCompile "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$versions.leakCanary"
    compile("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics") { transitive = true; }
    debugCompile "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    debugCompile "com.facebook.stetho:stetho:$versions.stetho"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$versions.stetho"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

configurations.all {
    resolutionStrategy {
        force "org.hamcrest:hamcrest-core:$versions.hamcrest"
        force "com.google.code.findbugs:jsr305:3.0.1"
    }

    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.android.support'
                && !details.requested.name.contains('multidex')) {
            details.useVersion "$versions.supportLibrary"
        }
    }
}

apply plugin: 'com.google.gms.google-services'